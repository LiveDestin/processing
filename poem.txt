//Mian code:

Mover m;

void setup() {
  size(640, 360);
  m = new Mover();
}

void draw() {
  background(255);

  PVector  gravity = new PVector(0, 0.2);
  m.applyForce(gravity);

  if (mousePressed) {
    PVector wind = new PVector(1, 0);
    m.applyForce(wind);
  }

  m.update();
  m.checkEdges();
  m.display();
}

//Ball class

class Mover {

  PVector position;
  PVector velocity;
  PVector acceleration;

  Mover() {
    position = new PVector(width/2,height/2);
    velocity = new PVector(0,0);
    acceleration = new PVector(0,0);
  }
  
  void applyForce(PVector f) {
    acceleration.add(f);
  }
  
  void update() {
    velocity.add(acceleration);
    position.add(velocity);
    acceleration.mult(0);
  }

  void display() {
    stroke(0);
    fill(127);
    ellipse(position.x,position.y,40,40);
  }

  void checkEdges() {

    if (position.x > width) {
      position.x = width;
      velocity.x *= -1;
    } else if (position.x < 0) {
      velocity.x *= -1;
      position.x = 0;
    }

    if (position.y > height) {
      velocity.y *= -1;
      position.y = height;
    }

  }

}
